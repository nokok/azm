plugins {
  id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
  mavenCentral()
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter', 'junit-vintage'
  }
}

tasks.withType(AbstractCompile)*.options*.encoding = tasks.withType(GroovyCompile)*.groovyOptions*.encoding = 'UTF-8'

def versionNumber = '0.1.7'

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['MyPublication']
  pkg {
    repo = 'azm'
    name = 'net.nokok'
    licenses = ['BSD-3-Clause']
    vcsUrl = 'https://github.com/nokok/azm'
    version {
      name = versionNumber
      desc = ''
      released = new Date()
      vcsTag = versionNumber
      attributes = [:]
    }
  }
}

dependencies {
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'net.nokok'
      artifactId 'azm'
      version versionNumber

      artifact sourceJar {
        classifier "sources"
      }
    }
  }
}